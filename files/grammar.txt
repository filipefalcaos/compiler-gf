PROGRAM = FUNCTIONS MAINFUNC

FUNCTIONS = funDecl id PARAMS RETURNTYPE BLOCK FUNCTIONS
FUNCTIONS = epsilon

MAINFUNC = main paramBeg paramEnd TYPE BLOCK

PARAMS = paramBeg PARAMSFAT
PARAMSFAT = paramEnd
PARAMSFAT = LISTPARAMS paramEnd

LISTPARAMS = TYPE NAME LISTPARAMSFAT
LISTPARAMSFAT = commaSep LISTPARAMS
LISTPARAMSFAT = epsilon

TYPE = typeInt
TYPE = typeFloat
TYPE = typeBool
TYPE = typeChar
TYPE = typeString
TYPE = typeEmpty
TYPE = typeNull

RETURNTYPE = TYPE RETURNTYPEFAT
RETURNTYPEFAT = arrayBeg arrayEnd
RETURNTYPEFAT = epsilon

NAME = id NAMEFAT
NAMEFAT = arrayBeg NAMEFAT2
NAMEFAT = epsilon
NAMEFAT2 = EXPR arrayEnd
NAMEFAT2 = arrayEnd

BLOCK = blockBeg CMDS blockEnd

CMDS = CMD CMDS
CMDS = epsilon

CMD = DECL endLine
CMD = id CMDFAT
CMD = PRINT endLine
CMD = READ endLine
CMD = IFELSE
CMD = WHILE
CMD = REPEAT
CMD = RETURN endLine
CMDFAT = arrayBeg EXPR arrayEnd ASSG endLine
CMDFAT = ASSG endLine
CMDFAT = FUNCCALL endLine
CMDFAT = epsilon

DECL = varDecl TYPE NAME

ASSG = opAssign VALUE

VALUE = ARRAY
VALUE = EXPR

ARRAY = arrayBeg ELEMENTS arrayEnd

ELEMENTS = EXPR ELEMENTSFAT
ELEMENTSFAT = commaSep ELEMENTS
ELEMENTSFAT = epsilon

CONSTANT = constNumInt
CONSTANT = constNumFloat
CONSTANT = constBool
CONSTANT = constChar
CONSTANT = constString

FUNCCALL = paramBeg LISTPARAMSCALL paramEnd

LISTPARAMSCALL = PARAMITEM LISTPARAMSCALLFAT
LISTPARAMSCALLFAT = commaSep PARAMITEM LISTPARAMSCALLFAT
LISTPARAMSCALLFAT = epsilon

PARAMITEM = CONSTANT
PARAMITEM = NAME PARAMITEMFAT
PARAMITEM = epsilon
PARAMITEMFAT = FUNCCALL
PARAMITEMFAT = epsilon

PRINT = cmdPrint paramBeg MESSAGE paramEnd

READ = cmdRead paramBeg NAME paramEnd

MESSAGE = constString MESSAGEFAT
MESSAGE = NAME MESSAGEFAT
MESSAGEFAT = opConcat MESSAGE
MESSAGEFAT = epsilon

IFELSE = IF ELIF ELSE

IF = cmdIf paramBeg EXPROR paramEnd BLOCK

ELIF = cmdElif paramBeg EXPROR paramEnd BLOCK ELIF
ELIF = epsilon

ELSE = cmdElse BLOCK
ELSE = epsilon

WHILE = cmdWhile paramBeg EXPROR paramEnd BLOCK

REPEAT = cmdRepeat id cmdIn paramBeg EXPR commaSep EXPR REPEATFAT paramEnd BLOCK
REPEATFAT = commaSep EXPROR
REPEATFAT = epsilon

RETURN = cmdReturn RETURNFAT
RETURNFAT = CONSTANT
RETURNFAT = NAME

EXPR = EXPROR
EXPROR = EXPRAND EXPRORFAT
EXPRORFAT = opOr EXPRAND EXPRORFAT
EXPRORFAT = epsilon

EXPRAND = EXPREQUALS EXPRANDFAT
EXPRANDFAT = opAnd EXPREQUALS EXPRANDFAT
EXPRANDFAT = epsilon

EXPREQUALS = EXPRREL EXPREQUALSFAT
EXPREQUALSFAT = opEquals EXPRREL EXPREQUALSFAT
EXPREQUALSFAT = epsilon

EXPRREL = EXPRADD EXPRRELFAT
EXPRRELFAT = opRel EXPRADD EXPRRELFAT
EXPRRELFAT = epsilon

EXPRADD = EXPRAMULT EXPRADDFAT
EXPRADDFAT = opAditiv EXPRAMULT EXPRADDFAT
EXPRADDFAT = epsilon

EXPRAMULT = EXPRUN EXPRAMULTFAT
EXPRAMULTFAT = opMult EXPRUN EXPRAMULTFAT
EXPRAMULTFAT = epsilon

EXPRUN = opUnaryNeg EXPRUN
EXPRUN = EXPRPARAM

EXPRPARAM = paramBeg EXPROR paramEnd
EXPRPARAM = EXPRVALUES

EXPRVALUES = constNumInt
EXPRVALUES = constNumFloat
EXPRVALUES = constBool
EXPRVALUES = constChar
EXPRVALUES = constString
EXPRVALUES = id EXPRVALUESFAT
EXPRVALUESFAT = arrayBeg EXPR arrayEnd
EXPRVALUESFAT = FUNCCALL
EXPRVALUESFAT = epsilon
