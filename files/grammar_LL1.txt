PROGRAM = FUNCTIONS MAINFUNC
        | DECL endLine GLDECL FUNCTIONS MAINFUNC

MAINFUNC = main paramBeg paramEnd TYPE BLOCK

GLDECL = DECL endLine GLDECL
       | epsilon

FUNCTIONS = funDecl id PARAMS RETURNTYPE BLOCK FUNCTIONS
          | epsilon

PARAMS = paramBeg PARAMSfat
PARAMSfat = paramEnd
          | LISTPARAMS paramEnd

LISTPARAMS = TYPE NAME LISTPARAMSr
LISTPARAMSr = commaSep LISTPARAMS
            | epsilon

TYPE = typeInt
     | typeFloat
     | typeBool
     | typeChar
     | typeString
     | typeEmpty
     | typeNull

RETURNTYPE = TYPE RETURNTYPEfat
RETURNTYPEfat = arrayBeg arrayEnd
              | epsilon

NAME = id NAMEfat
NAMEfat = arrayBeg NAMEfat2
        | epsilon

NAMEfat2 = EXPR arrayEnd
         | arrayEnd

BLOCK = blockBeg CMDS blockEnd

CMDS = CMD CMDS
     | epsilon

CMD = DECL endLine
    | id CMDfat
    | PRINT endLine
    | READ endLine
    | IFELSE
    | WHILE
    | REPEAT
    | RETURN endLine

CMDfat = arrayBeg EXPR arrayEnd ASSG endLine
       | ASSG endLine
       | FUNCCALL endLine
       | epsilon

DECL = varDecl TYPE NAME DECLfat
DECLfat = ASSG
        | epsilon

ASSG = opAssign ASSGfat
ASSGfat = TYPE castSep VALUE
        | VALUE

VALUE = ARRAY
      | EXPR

ARRAY = arrayValBeg ELEMENTS arrayValEnd
ELEMENTS = EXPR ELEMENTSr
ELEMENTSr = commaSep ELEMENTS
          | epsilon

FUNCCALL = paramBeg LISTPARAMSCALL paramEnd
LISTPARAMSCALL = PARAMITEM LISTPARAMSCALLr
LISTPARAMSCALLr = commaSep PARAMITEM LISTPARAMSCALLr
                | epsilon

PARAMITEM = EXPR
          | epsilon

PRINT = cmdPrint paramBeg EXPR paramEnd
READ = cmdRead paramBeg NAME paramEnd

IFELSE = IF ELIF ELSE
IF = cmdIf paramBeg EXPROR paramEnd BLOCK

ELIF = cmdElif paramBeg EXPROR paramEnd BLOCK ELIF
     | epsilon

ELSE = cmdElse BLOCK
     | epsilon

WHILE = cmdWhile paramBeg EXPROR paramEnd BLOCK
REPEAT = cmdRepeat id cmdIn paramBeg EXPR commaSep EXPR REPEATfat paramEnd BLOCK
REPEATfat = commaSep EXPROR
          | epsilon

RETURN = cmdReturn EXPR

EXPR = EXPROR
EXPROR = EXPRAND EXPRORr
EXPRORr = opOr EXPRAND EXPRORr
        | epsilon

EXPRAND = EXPREQUALS EXPRANDr
EXPRANDr = opAnd EXPREQUALS EXPRANDr
         | epsilon

EXPREQUALS = EXPRREL EXPREQUALSr
EXPREQUALSr = opEquals EXPRREL EXPREQUALSr
            | epsilon

EXPRREL = EXPRADD EXPRRELr
EXPRRELr = opRel EXPRADD EXPRRELr
         | epsilon

EXPRADD = EXPRAMULT EXPRADDr
EXPRADDr = opAditiv EXPRAMULT EXPRADDr
         | epsilon

EXPRAMULT = EXPRUN EXPRAMULTr
EXPRAMULTr = opMult EXPRUN EXPRAMULTr
           | epsilon

EXPRUN = opUnaryNeg EXPRUN
       | EXPRPARAM

EXPRPARAM = paramBeg EXPR paramEnd
          | EXPRVALUES

EXPRVALUES = constNumInt
           | constNumFloat
           | constBool
           | constChar ARRCONCAT
           | constString ARRCONCAT
           | id EXPRVALUESfat

EXPRVALUESfat = arrayBeg EXPR arrayEnd ARRCONCAT
              | FUNCCALL
              | CONCAT
              | epsilon

ARRCONCAT = CONCAT
          | epsilon

CONCAT = opConcat CONCATfat
CONCATfat = constString
          | constChar
          | id